/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zero the x input?
    Mux16(a=x, b=false, sel=zx, out=zeroX);

    // negate the x input?
    Not16(in=zeroX, out=notZeroX);
    Mux16(a=zeroX, b=notZeroX, sel=nx, out=negateZeroX);

    // zero the y input?
    Mux16(a=y, b=false, sel=zy, out=zeroY);

    // negate the y input?
    Not16(in=zeroY, out=notZeroY);
    Mux16(a=zeroY, b=notZeroY, sel=ny, out=negateZeroY);

    // compute out = x + y (if 1) or x & y (if 0)
    Add16(a=negateZeroX, b=negateZeroY, out=addXY);
    And16(a=negateZeroX, b=negateZeroY, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=computeXY);

    // negate the out output?
    Not16(in=computeXY, out=notComputeXY);
    Mux16(a=computeXY, b=notComputeXY, sel=no, out=out, out[0..7]=first, out[8..15]=last, out[15]=final15);

    // 1 if (out == 0), 0 otherwise
    Or8Way(in=first, out=Or07);
    Or8Way(in=last, out=Or816);
    Or(a=Or07, b=Or816, out=zrOr);
    Not(in=zrOr, out=zr);

    // 1 if (out < 0),  0 otherwise
    And(a=final15, b=true, out=ng);

}